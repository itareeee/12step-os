OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
        ramall(rwx)     : ORIGIN = 0xffbf20, LENGTH = 0x004000 /* 16KB */
        softvec(rw)     : ORIGIN = 0xffbf20, LENGTH = 0x000040 /* top of RAM, 64B */
        ram(rwx)        : ORIGIN = 0xffc020, LENGTH = 0x003f00
        stack(rw)       : ORIGIN = 0xffff00, LENGTH = 0x000000 /* end of RAM */
}

SECTIONS
{
        .softvec : {
                _softvec = .;
        } > softvec

        .text : {
                _text_start = . ;
                *(.text)
                _etext = . ;
        } > ram

        .rodata : {
                _rodata_start = . ;
                *(.strings)   
                *(.rodata)   
                *(.rodata.*)   
                _erodata = . ;
        } > ram

        .data : {
                _data_start = . ;
                *(.data)
                _edata = . ;
        } > ram

        .bss : {
                _bss_start = . ;
                *(.bss)
                *(COMMON)
                _ebss = . ;
        } > ram 

        . = ALIGN(4);
        _end = . ;

        .stack : {
                _stack = .;  
        } > stack
}
